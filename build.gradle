/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2018-2020 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = '**undefined**'
if (!project.hasProperty('bintrayApiKey'))   ext.bintrayApiKey   = '**undefined**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name          = 'Kordamp parent'
        description   = 'Parent for Kordamp projects'
        vendor        = 'Kordamp'
        inceptionYear = '2018'
        tags          = ['kordamp', 'pom', 'build']

        links {
            website      = "https://github.com/kordamp/${project.rootProject.name}"
            issueTracker = "https://github.com/kordamp/${project.rootProject.name}/issues"
            scm          = "https://github.com/kordamp/${project.rootProject.name}.git"
        }

        scm {
            url                 = "https://github.com/kordamp/${project.rootProject.name}"
            connection          = "scm:git:https://github.com/kordamp/${project.rootProject.name}.git"
            developerConnection = "scm:git:git@github.com:kordamp/${project.rootProject.name}.git"
        }

        specification  { enabled = false }

        implementation { enabled = false }

        people {
            person {
                id    = 'aalmiray'
                name  = 'Andres Almiray'
                url   = 'http://andresalmiray.com/'
                roles = ['developer']
                properties = [
                    twitter: 'aalmiray',
                    github : 'aalmiray'
                ]
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url  = "${project.rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "${project.rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    docs {
        javadoc {
            enabled = false
        }

        groovydoc {
            replaceJavadoc = true
            options {
                link 'https://docs.gradle.org/5.0/javadoc/', 'org.gradle.'
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    bintray {
        enabled      = false
        userOrg      = 'kordamp'
        repo         = 'maven'
        name         = rootProject.name
        publish      = config.release
        credentials {
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
    }

    publishing {
        signing             = false
        releasesRepository  = 'localRelease'
        snapshotsRepository = 'localSnapshot'
    }
}

allprojects {
    repositories {
        jcenter()
        gradlePluginPortal()
        mavenLocal()
    }
}

projects.subprojects {
    dir('plugins') {
        config {
            info {
                description = project_description
            }

            plugins {
                plugin {
                    name                = project.name
                    id                  = project.pluginId
                    implementationClass = project.pluginImplementationClass
                }
            }
        }

        dependencies {
            compileOnly gradleApi()
        }

        normalization {
            runtimeClasspath {
                ignore('/META-INF/MANIFEST.MF')
            }
        }
    }
}